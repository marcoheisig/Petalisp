;;;; Â© 2016-2023 Marco Heisig         - license: GNU AGPLv3 -*- coding: utf-8 -*-

(in-package #:petalisp.graphviz)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Classes

(defclass class-diagram (any-graph)
  ())

(defclass direct-subclass-edge (any-edge)
  ())

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Connectivity

(defmethod graphviz-potential-edges append
    ((graph class-diagram)
     (class class))
  (list (make-instance 'direct-subclass-edge)))

(defmethod graphviz-incoming-edge-origins
    ((graph class-diagram)
     (edge direct-subclass-edge)
     (class class))
  (closer-mop:class-direct-subclasses class))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Graph Appearance

(defmethod graphviz-graph-attributes
    ((graph class-diagram))
  `(:rankdir "BT"))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Node Appearance

(defmethod cl-dot:graph-object-node
    ((graph class-diagram)
     (class class))
  (make-instance 'cl-dot:node
    :attributes
    `(:shape :record
      :style :filled
      :fillcolor "gray95"
      :label
      (:html
       ()
       (:table
        ((:border "0") (:cellborder "0") (:cellspacing "0"))
        (:tr () (:td ((:colspan "2") (:align "center")) (:b () ,(stringify (class-name class)))))
        ,@(loop for slot in (closer-mop:class-direct-slots class)
                for name = (string-downcase (closer-mop:slot-definition-name slot))
                for type = (closer-mop:slot-definition-type slot)
                collect
                `(:tr ()
                      (:td ((:align "left"))
                           ,(format nil "~A :"
                                    (if (and (plusp (length name))
                                             (char= (elt name 0) #\%))
                                        (subseq name 1)
                                        name)))
                      (:td ((:align "left")) ,(stringify type)))))))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Edge Appearance

(defmethod graphviz-edge-attributes
    ((graph class-diagram)
     (edge direct-subclass-edge)
     (from class)
     (to class)
     edge-number)
  `(:arrowhead :onormal :style :dashed))
